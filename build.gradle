plugins {
    id 'fabric-loom' version '1.8.13'
    id 'maven-publish'
}

apply from: 'versions.gradle'

def mcVersion = project.hasProperty('mcVersion') ? project.property('mcVersion') : '1.20.1'
def versionConfig = minecraftVersions[mcVersion]

if (versionConfig == null) {
    throw new GradleException("Unknown Minecraft version: ${mcVersion}. Available versions: ${minecraftVersions.keySet()}")
}

version = "${project.mod_version}+mc${mcVersion}"
group = project.maven_group

base {
    archivesName = "${project.archives_base_name}-${mcVersion}"
}

repositories {
    maven { url 'https://maven.fabricmc.net/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${versionConfig.minecraft}"
    mappings "net.fabricmc:yarn:${versionConfig.yarn}:v2"
    modImplementation "net.fabricmc:fabric-loader:${versionConfig.loader}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${versionConfig.fabric}"

    include(implementation('com.squareup.okhttp3:okhttp:4.12.0'))
    include(implementation('com.squareup.okio:okio:3.6.0'))
    include(implementation('com.squareup.okio:okio-jvm:3.6.0'))
    include(implementation('org.jetbrains.kotlin:kotlin-stdlib:1.9.20'))
    include(implementation('com.google.code.gson:gson:2.10.1'))
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
    }
}
